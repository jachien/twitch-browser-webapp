/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.0/userguide/tutorial_java_projects.html
 */


// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.springframework.boot'


group="org.jchien.twitchbrowser"
version="1.0"
sourceCompatibility="1.8"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE'
    }
}

jar {
    baseName = 'twitch-browser-webapp'
    version = project.version
}

repositories {
    mavenCentral()
    mavenLocal()
}

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.4.0' // CURRENT_GRPC_VERSION
def log4j2Version = '2.8.2'
def springVersion = '1.5.6.RELEASE'

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.24'

    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4j2Version}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4j2Version}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4j2Version}"

    // spring boot using log4j2 instead of logback
    // https://stackoverflow.com/a/28526317/7166633
    [
        [group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: "${springVersion}"],
        [group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springVersion}"],
        [group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "${springVersion}"],
        [group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker', version: "${springVersion}"]
    ].each { dep ->
        compile(dep) {
            exclude module: "spring-boot-starter-logging"
            exclude module: "logback-classic"
        }
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: "${springVersion}"

    compile 'org.jchien.twitchbrowser:twitch-browser-service:1.0'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}

apply plugin: 'idea'


// https://docs.gradle.org/4.0/userguide/publishing_maven.html
apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}